apply plugin: rootProject.ext.plugins.maven
apply plugin: rootProject.ext.plugins.bintray

version = rootProject.ext.maven.version

def siteUrl = rootProject.ext.maven.siteUrl
def gitUrl = rootProject.ext.maven.gitUrl

group = rootProject.ext.maven.group

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging rootProject.ext.maven.packaging
                name rootProject.ext.maven.name
                description rootProject.ext.maven.description
                url siteUrl
                licenses {
                    license {
                        name rootProject.ext.maven.licenseName
                        url rootProject.ext.maven.licenseUrl
                    }
                }
                developers {
                    developer {
                        id rootProject.ext.maven.developerId
                        name rootProject.ext.maven.developerName
                        email rootProject.ext.maven.developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = rootProject.ext.maven.bintrayUser
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = rootProject.ext.maven.bintrayRepo
        name = rootProject.ext.maven.binrayLibrary
        userOrg = rootProject.ext.maven.bintrayUser
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = [rootProject.ext.maven.bintrayLicense]
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}